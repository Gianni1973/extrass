import groovy.io.FileType

import javax.crypto.Cipher
import javax.crypto.SecretKey
import javax.crypto.spec.IvParameterSpec
import javax.crypto.spec.SecretKeySpec

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: 'ThemerConstants.gradle'
apply plugin: 'kotlin-android-extensions'

ext {
    // Themers: DO NOT MODIFY
    byte[] key = new byte[16]
    new Random().nextBytes(key)
    KEY = key
    byte[] iv = new byte[16]
    new Random().nextBytes(iv)
    IV_KEY = iv
}

android {
    signingConfigs {
        'new' {
            storeFile file('D:\\SigninKeys\\persubstratum.jks')
            storePassword 'trottola'
            keyAlias 'key0'
            keyPassword 'trottola'
        }
    }

    compileSdkVersion 28

    defaultConfig {
        // If you're planning to change up the package name, ensure you have read the readme
        // thoroughly!
        applicationId "aaextras.rizzo.gabriele"
        // We are only supporting Nougat and above, all new changes will incorporate Nougat changes
        // to the substratum repo rather than anything lower. Keep targetSdkVersion the same.
        minSdkVersion 24
        // Both versions must be changed to increment on Play Store/user's devices
        versionCode 44
        versionName "5.2"
        // Themers: DO NOT MODIFY
        buildConfigField "boolean", "SUPPORTS_THIRD_PARTY_SYSTEMS", "" + SUPPORTS_THIRD_PARTY_SYSTEMS
        buildConfigField "boolean", "ENABLE_APP_BLACKLIST_CHECK", "" + ENABLE_APP_BLACKLIST_CHECK
        buildConfigField "boolean", "ALLOW_THIRD_PARTY_SUBSTRATUM_BUILDS", "" + ALLOW_THIRD_PARTY_SUBSTRATUM_BUILDS
        buildConfigField "String", "IV_KEY", "\"" + IV_KEY + "\""
        buildConfigField "byte[]", "DECRYPTION_KEY", String.valueOf("\"" + KEY + "\"").replace("\"", "").replace("[", "{").replace("]", "}")
        buildConfigField "byte[]", "IV_KEY", String.valueOf("\"" + IV_KEY + "\"").replace("\"", "").replace("[", "{").replace("]", "}")

    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Themers: DO NOT MODIFY
            buildConfigField "boolean", "ENFORCE_GOOGLE_PLAY_INSTALL", "false"
            buildConfigField "String", "BASE_64_LICENSE_KEY", "\"\""
            buildConfigField "String", "APK_SIGNATURE_PRODUCTION", "\"\""
            jniDebuggable = false
            debuggable = false
            renderscriptDebuggable = false
            signingConfig signingConfigs.new
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Themers: DO NOT MODIFY
            buildConfigField "boolean", "ENFORCE_GOOGLE_PLAY_INSTALL", "" + ENFORCE_GOOGLE_PLAY_INSTALL
            buildConfigField "String", "BASE_64_LICENSE_KEY", "\"" + BASE_64_LICENSE_KEY + "\""
            buildConfigField "String", "APK_SIGNATURE_PRODUCTION", "\"" + APK_SIGNATURE_PRODUCTION + "\""
            jniDebuggable = false
            debuggable = false
            renderscriptDebuggable = false
            signingConfig signingConfigs.new
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {

            def SEP = "_"
            def flavor = variant.productFlavors[0].name
            def version = variant.versionName
            def newApkName
            if (flavor == "nonlegacy") {
                newApkName = "Extras" + "4AA" + SEP + version + SEP + "release" + ".apk"
            } else if (flavor == "legacy") {
                newApkName = "Extras" + "4AA" + SEP + "LEGACY" + version + SEP + "release" + ".apk"
            } else {
                newApkName = flavor + "4AA" + SEP + version + SEP + "release" + ".apk"
            }

            outputFileName = "../../../APKS/" + new File(newApkName)
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        legacy{
            assets.srcDirs += 'src/base/assets/'
        }
        nonlegacy {
            assets.srcDirs += 'src/base/assets/'
        }
    }
    flavorDimensions 'dim'
    productFlavors {
        legacy {
            dimension 'dim'
            applicationId 'aaextras.rizzo.gabriele.legacy'
            minSdkVersion 24
            legacy.versionName = defaultConfig.versionName + "L"
            manifestPlaceholders.substratumLauncherActivity = "aaextras.rizzo.gabriele.legacy.SubstratumLauncher"
            manifestPlaceholders.LauncherActivity = "aaextras.rizzo.gabriele.legacy.Launcher"
        }
        nonlegacy {
            dimension 'dim'
            minSdkVersion 26
            nonlegacy.versionName = defaultConfig.versionName
            manifestPlaceholders.substratumLauncherActivity = "aaextras.rizzo.gabriele.SubstratumLauncher"
            manifestPlaceholders.LauncherActivity = "aaextras.rizzo.gabriele.Launcher"
        }
        icons {
            dimension 'dim'
            applicationId 'aaextras.rizzo.gabriele.icons'
            minSdkVersion 26
            icons.versionName = "2.5I"
            manifestPlaceholders.substratumLauncherActivity = "aaextras.rizzo.gabriele.icons.SubstratumLauncher"
            manifestPlaceholders.LauncherActivity = "aaextras.rizzo.gabriele.icons.Launcher"
        }
        animations {
            dimension 'dim'
            applicationId 'aaextras.rizzo.gabriele.animations'
            minSdkVersion 24
            animations.versionName = "1.1A"
            manifestPlaceholders.substratumLauncherActivity = "aaextras.rizzo.gabriele.animations.SubstratumLauncher"
            manifestPlaceholders.LauncherActivity = "aaextras.rizzo.gabriele.animations.Launcher"
        }
    }
}

dependencies {
    implementation 'com.github.jetradarmobile:android-snowfall:1.2.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.github.javiersantos:PiracyChecker:1.2.5'
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
    implementation('androidx.appcompat:appcompat:1.1.0')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}

// Themers: DO NOT MODIFY ANYTHING BELOW
task encryptAssets {
    if (!shouldEncrypt()) {
        println("Skipping assets encryption...")
        return
    }

    def tempAssets = new File(getProjectDir(), "/src/main/assets-temp")
    if (!tempAssets.exists()) {
        println("Encrypting duplicated assets, don't worry, your original assets are safe...")
        def list = []
        def dir = new File(getProjectDir(), "/src/main/assets")
        dir.eachFileRecurse(FileType.FILES) { file ->
            list << file

            FileInputStream fis = new FileInputStream(file)
            File fo = new File(file.getAbsolutePath().replace("assets", "assets-temp"))
            fo.getParentFile().mkdirs()
            FileOutputStream fos = new FileOutputStream(fo)
            byte[] buffer = new byte[4096]
            int n
            while ((n = fis.read(buffer)) != -1) {
                fos.write(buffer, 0, n)
            }
            fis.close()
            fos.close()
        }

        list.each {
            if (it.getAbsolutePath().contains("overlays")) {
                Cipher cipher = Cipher.getInstance("AES/CBC/PKCS5Padding")
                SecretKey secret = new SecretKeySpec(KEY, "AES")
                IvParameterSpec iv = new IvParameterSpec(IV_KEY)

                cipher.init(Cipher.ENCRYPT_MODE, secret, iv)
                FileInputStream fis = new FileInputStream(it)
                FileOutputStream fos = new FileOutputStream(it.getAbsolutePath() + ".enc")

                byte[] input = new byte[64]
                int bytesRead
                while ((bytesRead = fis.read(input)) != -1) {
                    byte[] output = cipher.update(input, 0, bytesRead)
                    if (output != null) {
                        fos.write(output)
                    }
                }
                byte[] output = cipher.doFinal()
                if (output != null) {
                    fos.write(output)
                }
                fis.close()
                fos.flush()
                fos.close()

                it.delete()
            }
        }
    } else {
        throw new RuntimeException("Old temporary assets found! Try and do a clean project.")
    }
}

project.afterEvaluate {
    preBuild.dependsOn encryptAssets
}

gradle.buildFinished {
    def tempAssets = new File(getProjectDir(), "/src/main/assets-temp")
    if (tempAssets.exists()) {
        println("Cleaning duplicated encrypted assets, not your decrypted assets...")
        def encryptedAssets = new File(getProjectDir(), "src/main/assets")
        encryptedAssets.deleteDir()

        tempAssets.eachFileRecurse(FileType.FILES) { file ->
            FileInputStream fis = new FileInputStream(file)
            File fo = new File(file.getAbsolutePath().replace("assets-temp", "assets"))
            fo.getParentFile().mkdirs()
            FileOutputStream fos = new FileOutputStream(fo)
            byte[] buffer = new byte[4096]
            int n
            while ((n = fis.read(buffer)) != -1) {
                fos.write(buffer, 0, n)
            }
            fis.close()
            fos.close()
        }
        tempAssets.deleteDir()
    }
}

boolean shouldEncrypt() {
    ArrayList<String> tasks = project.gradle.startParameter.taskNames
    return SHOULD_ENCRYPT_ASSETS && Arrays.toString(tasks).toLowerCase().contains("PIPIPIP")
}